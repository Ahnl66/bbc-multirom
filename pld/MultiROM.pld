Name        MultiROM ;
PartNo      16V8 ;
Date        2023-02-09 ;
Revision    01 ;
Designer    Richard Halkyard ;
Company     None ;
Assembly    MultiROM Cartridge for BBC Master and Acorn Electron ;
Location    U2 ;
Device      g16v8 ;

/* *************** INPUT PINS *******************************************/
PIN 1   = !A16H ;   /* Bank-select switch input for high ROM */
PIN 2   = !A15H ;   /* Bank-select switch input for high ROM */
PIN 3   = !A16L ;   /* Bank-select switch input for low ROM */
PIN 4   = !A15L ;   /* Bank-select switch input for low ROM */
PIN 5   = ROMQA ;   /* ROM low/high select */
PIN 6   = MRnW ;    /* BBC Master Read/Write control. Unused on Electron */
PIN 7   = !MADET ;  /* Master/Electron sense. Low = Master */
PIN 8   = !ROMOE ;  /* Cartridge select line */
PIN 9   = CSRnW ;   /* Master: catridge select. Electron: Read/Write control */
PIN 11  = Phi2 ;    /* Processor clock */

/* *************** OUTPUT PINS ******************************************/
PIN 14  = !CS ;      /* Flash chip select */
PIN 15  = !OE ;      /* Flash output enable */
PIN 17  = A15 ;      /* Flash address line A15 */
PIN 18  = A16 ;      /* Flash address line A16 */
PIN 19  = !PGM ;     /* Flash program enable */

/* *************** LOGIC EQUATIONS  *************************************/
/* Address line switches                                                */
/*      Low ROM                     High ROM                            */
A15     = !ROMQA & A15L             # ROMQA & A15H ;
A16     = !ROMQA & A16L             # ROMQA & A16H ;

/* Selection logic                                                      */
/*      Master                      Electron                            */
CS      = MADET & CSRnW & Phi2      # !MADET & ROMOE & Phi2 ;
OE      = MADET & ROMOE & MRnW      # !MADET & ROMOE & CSRnW ;
PGM     = MADET & !MRnW             # !MADET & !CSRnW ;
